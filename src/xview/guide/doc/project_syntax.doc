@(#)project_syntax.doc	1.3 91/10/15 Copyright 1989 Sun Microsystems.

	Extensions to the Guide Interface Language Syntax for Projects.

BASIC TYPES:
------------

<action>			::= ( 	:from 		<target-name>
					:when 		<name-list>
					:to   		<target-name>
					:function_type 	<function-type>
					:arg_type	<target-name>
					:action		<name-list> )
<actions-list>			::= ( { <action> } )

<arg-type>			::= :void | :integer | :string
<arg-type-list>			::= ( { <arg-type } )

<character>			::= <any printable ascii character>

<character-list>		::= ( { <character> } )

<function-type>			::= :user-type | :func-type | :code-type

<gil-file>			::= <name>.G

<name>				::= { [A-Za-z0-9_-] } | nil

<name-list>			::= ( { <name> } )

<string>			::= "{ <character> }"

<string-list>			::= ( { <string> } )

<target-name>			::= ( <gil-file> { <name> } )


MISCELLANEOUS:
--------------

<comment>			::= ; { <character> }

<version-number>		::= 3

<gil-header>			::= ;GIL-<version-number> { <comment> }


ATTRIBUTE/VALUE PAIRS:
----------------------

<actions-attr>			::= :actions <actions-list>

<interfaces-attr>		::= :interfaces { <gil-file> }

<root-window-attr>		::= :root_window <name>


FILE:
-----

<proj-attr-list>		::= (<interfaces-attr> 
				     <actions-attr> 
				     <root-window-attr>)

<project-file>			::= <gil-header> <proj-attr-list>


