@(#)gil_syntax.doc	2.26 91/10/15 Copyright 1989 Sun Microsystems.

		Guide Interface Language Syntax

BASIC TYPES:
------------

<arg-type>			::= :void | :integer | :string
<arg-type-list>			::= ( { <arg-type } )

*** <action>			::= ( 	:from 		<name-list>
					:when 		<name-list>	
					:to   		<name-list>
					:function_type 	<function-type>
					:arg_type	<arg-type>
					:action		<name-list> | <compound-string> )
<actions-list>			::= ( { <action> } )

<base-window-state>		::= :open | :iconic | :invisible

<boolean>			::= t | nil
<boolean-list>			::= ( { <boolean> } )

<character>			::= <any printable ascii character>
<character-list>		::= ( { <character> } )

<column-alignment-type>		::= :left-edges |
				    :labels | 
				    :vertical-centers |
				    :right-edges

<compass-point>			::= :north-west |
				    :north |
				    :north-east |
				    :west |
				    :east |
				    :center |
				    :south-west |
				    :south |
				    :south-east


<control-object-state>		::= :active | :inactive | :invisible

<string>			::= "{ <character> }"
<compound-string>		::= ({ <character> })
<string-list>			::= ( { <string> } )

<button-type>			::= :normal | :abbreviated

<drawing-model-type>		::= :xview | :xwindows | :postscript

* <event-type>			::= :keyboard |
				    :keyboard-left |
				    :keyboard-right |
				    :keyboard-top |
				    :mouse |
				    :mouse-drag |
				    :mouse-enter |
				    :mouse-exit |
				    :mouse-move

* <event-type-list>		::= ( { <event-type> } )

*** <function-type>		::= :user-defined | ExecuteCode

<group-type>			::= :row | :column | :row-column | nil

<label-type>			::= :string | :glyph

<label-type-list>		::= ( { <label-type> } )

<label-layout-type>		::= :horizontal | :vertical

** <mapped-attr>		::= :mapped <boolean>

<menu-type>			::= :command | :exclusive | :nonexclusive

<menu-item-state>		::= :active | :inactive
<menu-item-state-list>		::= ( { <menu-item-state> } )

<name>				::= { [A-Za-z0-9_-] } | nil
<name-list>			::= ( { <name> } )

<orientation-type>		::= :horizontal | :vertical

<pane-state>			::= :visible | :invisible

<popup-window-state>		::= :open | :invisible

<row-alignment-type>		::= :top-edges |
				    :horizontal-centers |
				    :bottom-edges

<setting-type>			::= :exclusive | :nonexclusive | :check | :stack

<text-type>			::= :alphanumeric | :numeric | :multiline

MISCELLANEOUS:
--------------

<comment>			::= ; { <character> }

<version-number>		::= 3

<gil-header>			::= ;GIL-<version-number> { <comment> }


ATTRIBUTE/VALUE PAIRS:
------------------------

<actions-attr>			::= :actions <actions-list>

<anchor-object-attr>		::= :anchor-object <name>

<anchor-point-attr>		::= :anchor-point <compass-point>

<background-color-attr>		::= :background-color <string>

<base-window-state-attr>	::= :initial-state <base-window-state>

<busy-drop-glyph-attr>		::= :busy-drop-glyph <string>

<button-type-attr>		::= :button-type <button-type>

<choices-attr>			::= :choices <string-list>

<choice-label-types-attr>	::= :choice-label-types <label-type-list>

<choice-colors-attr>		::= :choice-colors <string-list>

<choice-defaults-attr>		::= :choice-defaults <boolean-list>

<columns-attr>			::= :columns <integer>

<column-alignment-attr>		::= :column-alignment <column-alignment-type>

<constant-width-attr>		::= :constant-width <boolean>

<control-object-state-attr>	::= :initial-state <control-object-state>

<default-drop-site-attr>	::= :default-drop-site <boolean>

* <done-handler-attr>		::= :done-handler <name>

<dnd-accept-cursor-attr>	::= :dnd-accept-cursor <string>

<dnd-accept-cursor-xhot-attr>	::= :dnd-accept-cursor-xhot <integer>

<dnd-accept-cursor-yhot-attr>	::= :dnd-accept-cursor-yhot <integer>

<dnd-cursor-attr>		::= :dnd-cursor <string>

<dnd-cursor-xhot-attr>		::= :dnd-cursor-xhot <integer>

<dnd-cursor-yhot-attr>		::= :dnd-cursor-yhot <integer>

<draggable-attr>		::= :draggable <boolean>

<drawing-model-attr>		::= :drawing-model <drawing-model-type>

<droppable-attr>		::= :droppable <boolean>

<drop-target-width-attr>	::= :drop-target-width <integer>

** *** <events-attr>		::= :events <event-type-list>

* <event-handler-attr>		::= :event-handler <name>

<foreground-color-attr>		::= :foreground-color <string>

<group-type-attr>		::= :group-type <group-type>

<height-attr>			::= :height <integer>

<help-attr>			::= :help <string>

<horizontal-offset-attr>	::= :horizontal-offset <integer>

<horizontal-scrollbar-attr>	::= :horizontal-scrollbar <boolean>

<horizontal-spacing-attr>	::= :horizontal-spacing <integer>

<icon-file-attr>		::= :icon-file <string>

<icon-label-attr>		::= :icon-label <string>

<icon-mask-file-attr>		::= :icon-mask-file <string>

<initial-integer-value-attr>	::= :initial-value <integer>

<initial-list-values-attr>	::= :initial-list-values <string-list>

<initial-list-glyphs-attr>	::= :initial-list-glyphs <string-list>

<initial-selections-attr>	::= :initial-selections <boolean-list>

<label-attr>			::= :label <string>

<label-type-attr>		::= :label-type <label-type>

<label-bold-attr>		::= :label-bold <boolean>

<label-layout-type-attr>	::= :layout-type <label-layout-type>

<max-value-attr>		::= :max-value <integer>

<members-attr>			::= :members <name-list>

* <menu-handler-attr>		::= :menu-handler <name>

<menu-item-colors-attr>		::= :menu-item-colors <string-list>

<menu-item-defaults-attr>	::= :menu-item-defaults <boolean-list>

* <menu-item-handlers-attr>	::= :menu-item-handlers <name-list>

<menu-item-labels-attr>		::= :menu-item-labels <string-list>

<menu-item-label-types-attr>	::= :menu-item-label-types <label-type-list>

<menu-item-menus-attr>		::= :menu-item-menus <name-list>

<menu-item-states-attr>		::= :menu-item-states <menu-item-state-list>

* <menu-name-attr>		::= :menu <name>

<menu-title-attr>		::= :menu-title <string>

<menu-type-attr>		::= :menu-type <menu-type>

<min-value-attr>		::= :min-value <integer>

<multiple-selections-attr>	::= :multiple-selections <boolean>

<name-attr>			::= :name <name>

<normal-drop-glyph-attr>	::= :normal-drop-glyph <string>

* <notify-handler-attr>		::= :notify-handler <name>

<orientation-attr>		::= :orientation <orientation-attr>

<owner-attr>			::= :owner <name>

<pane-state-attr>		::= :initial-state <pane-state>

<pinnable-attr>			::= :pinnable <boolean>

<pinned-attr>			::= :pinned <boolean>

<popup-window-state-attr>	::= :initial-state <popup-window-state>

<read-only-attr>		::= :read-only <boolean>

<reference-point-attr>		::= :reference-point <compass-point>

* <repaint-proc-attr>		::= :repaint-proc <name>

<resizable-attr>		::= :resizable <boolean>

<rows-attr>			::= :rows <integer>

<row-alignment-attr>		::= :row-alignment <row-alignment-type>

<scrollable-height-attr>	::= :scrollable-height <integer>

<scrollable-width-attr>		::= :scrollable-width <integer>

<selection-required-attr>	::= :selection-required <boolean>

<setting-type-attr>		::= :setting-type <setting-type>

<show-border-attr>		::= :show-border <boolean>

<show-endboxes-attr>		::= :show-endboxes <boolean>

<show-footer-attr>		::= :show-footer <boolean>

<show-range-attr>		::= :show-range <boolean>

<show-value-attr>		::= :show-value <boolean>

<slider-width-attr>		::= :slider-width <integer>

<stored-length-attr>		::= :stored-length <integer>

<text-initial-value-attr>      	::= :initial-value <string> | <integer> 

<text-type-attr>		::= :text-type <text-type>

<ticks-attr>			::= :ticks <integer>

<title-attr>			::= :title <string>

<user-data-attr>		::= :user-data <character-list>

<value-length-attr>		::= :value-length <integer>

<value-underlined-attr>		::= :value-underlined <boolean>

<value-x-attr>			::= :value-x <integer>

<value-y-attr>			::= :value-y <integer>

<vertical-offset-attr>		::= :vertical-offset <integer>

<vertical-scrollbar-attr>	::= :vertical-scrollbar <boolean>

<vertical-spacing-attr>		::= :vertical-spacing <integer>

<width-attr>			::= :width <integer>

<x-attr>			::= :x <integer>

<y-attr>			::= :y <integer>


ELEMENTS:
--------

<base-window>			::= :type :base-window
				    <name-attr>
				    <owner-attr>
				    <width-attr>
				    <height-attr>
				    <background-color-attr>
				    <foreground-color-attr>
				    <label-attr>
				    <base-window-state-attr>
				    <show-footer-attr>
				    <resizable-attr> 
				    <icon-file-attr>
				    <icon-label-attr>
				    <icon-mask-file-attr>
				 *  <event-handler-attr> 
				    <user-data-attr>
				    <actions-attr>

<button>			::= :type :button
				    <name-attr> 
				    <owner-attr> 
				    <help-attr> 
				    <x-attr> 
				    <y-attr> 
				    <button-type-attr> 
				    <constant-width-attr>
				    <width-attr> 
				    <height-attr> 
				    <foreground-color-attr> 
				    <label-attr> 
				    <label-type-attr>
				    <control-object-state-attr>
				 *  <menu-name-attr> 
				 *  <notify-handler-attr> 
				 *  <event-handler-attr>
				    <user-data-attr>
				    <actions-attr>

<canvas-pane>			::= :type :canvas-pane
				    <name-attr> 
				    <owner-attr> 
				    <help-attr> 
				    <x-attr> 
				    <y-attr> 
				    <width-attr> 
				    <height-attr> 
				    <background-color-attr> 
				    <foreground-color-attr> 
				    <pane-state-attr>
				    <draggable-attr>
				    <droppable-attr>
				    <default-drop-site-attr>
				 *  <menu-name-attr> 
				    <horizontal-scrollbar-attr>
				    <scrollable-width-attr> 
				    <vertical-scrollbar-attr>
				    <scrollable-height-attr> 
				 *  <repaint-proc-attr> 
				 *  <event-handler-attr> 
				    <drawing-model-attr>
				    <user-data-attr>
				    <actions-attr>

<control-area>			::= :type :control-area 
				    <name-attr>
				    <owner-attr>
				    <help-attr>
				    <x-attr>
				    <y-attr>
				    <width-attr>
				    <height-attr>
				    <background-color-attr>
				    <foreground-color-attr>
				    <pane-state-attr>
				    <show-border-attr>
				 *  <menu-name-attr> 
				 *  <event-handler-attr> 
				    <user-data-attr>
				    <actions-attr>

<drop-target>			::= :type :drop-target
				    <name-attr>
				    <owner-attr>
				    <help-attr>
				    <x-attr>
				    <y-attr>
				    <width-attr>
				    <height-attr>
				    <drop-target-width-attr>
				    <default-drop-site-attr>
				    <draggable-attr>
				    <droppable-attr>
				    <label-attr> 
				    <label-type-attr> 
				    <normal-drop-glyph-attr>
				    <busy-drop-glyph-attr>
				    <dnd-cursor-attr>
				    <dnd-cursor-xhot-attr>
				    <dnd-cursor-yhot-attr>
				    <dnd-accept-cursor-attr>
				    <dnd-accept-cursor-xhot-attr>
				    <dnd-accept-cursor-yhot-attr>
				    <foreground-color-attr>
				    <control-object-state-attr>
				 *  <notify-handler-attr>
				 *  <event-handler-attr> 
				    <user-data-attr>
				    <actions-attr>

<gauge>				::= :type :gauge
				    <name-attr>
				    <owner-attr>
				    <help-attr>
				    <x-attr>
				    <y-attr>
				    <width-attr>
				    <height-attr>
				    <value-x-attr> 
				    <value-y-attr> 
				    <slider-width-attr> 
				    <foreground-color-attr>
				    <label-attr> 
				    <label-type-attr> 
				    <control-object-state-attr>
				    <label-layout-type-attr> 
				    <orientation-attr>
				    <show-range-attr>
				    <min-value-attr> 
				    <max-value-attr> 
				    <initial-integer-value-attr>
				 *  <event-handler-attr> 
				    <user-data-attr>
				    <actions-attr>

<group>				::= :type :group
				    <name-attr>
				    <x-attr>
				    <y-attr>
				    <group-type-attr>
				    <members-attr>
				    <rows-attr>
				    <row-alignment-attr>
				    <columns-attr>
				    <column-alignment-attr>
				    <horizontal-spacing-attr>
				    <vertical-spacing-attr>
				    <anchor-object-attr>
				    <anchor-point-attr>
				    <reference-point-attr>
				    <horizontal-offset-attr>
				    <vertical-offset-attr>

<menu>				::= :type :menu
				    <name-attr>
				    <help-attr>
				    <columns-attr> 
				    <menu-type-attr> 
				 *  <menu-handler-attr> 
				    <menu-title-attr> 
				    <menu-item-labels-attr> 
				    <menu-item-label-types-attr> 
				    <menu-item-defaults-attr> 
				    <menu-item-colors-attr> 
				 *  <menu-item-handlers-attr> 
				    <menu-item-menus-attr> 
				    <menu-item-states-attr>
				    <initial-selections-attr>
				    <pinnable-attr>
				    <user-data-attr>
				    <actions-attr>

<message>			::= :type :message
				    <name-attr>
				    <owner-attr>
				    <help-attr>
				    <x-attr>
				    <y-attr>
				    <width-attr>
				    <height-attr>
				    <foreground-color-attr>
				    <label-attr> 
				    <label-type-attr> 
				    <label-bold-attr> 
				    <control-object-state-attr>
				 *  <event-handler-attr> 
				    <user-data-attr>
				    <actions-attr>

<popup-window>			::= :type :popup-window
				    <name-attr>
				    <owner-attr>
				    <width-attr>
				    <height-attr>
				    <background-color-attr>
				    <foreground-color-attr>
				    <label-attr> 
				    :label-type :string
				    <popup-window-state-attr>
				    <show-footer-attr>
				    <resizable-attr> 
				    <pinned-attr> 
				 *  <done-handler-attr> 
				 *  <event-handler-attr> 
				    <user-data-attr>
				    <actions-attr>

<scrolling-list>		::= :type :scrolling-list
				    <name-attr>
				    <owner-attr>
				    <help-attr>
				    <x-attr>
				    <y-attr>
				    <width-attr>
				    <height-attr>
				    <value-x-attr> 
				    <value-y-attr> 
				    <foreground-color-attr>
				    <label-attr>
				    <title-attr>
				    <label-type-attr>
				    <label-layout-type-attr> 
				    <control-object-state-attr>
				    <droppable-attr>
				    <default-drop-site--attr>
				    <rows-attr> 
				    <read-only-attr> 
				    <multiple-selections-attr> 
				    <selection-required-attr> 
				 *  <menu-name-attr>
				 *  <notify-handler-attr> 
				 *  <event-handler-attr> 
				    <user-data-attr>
				    <initial-selections-attr>
				    <initial-list-values-attr>
				    <initial-list-glyphs-attr>
				    <actions-attr>

<setting>			::= :type :setting
				    <name-attr>
				    <owner-attr>
				    <help-attr>
				    <x-attr>
				    <y-attr>
				    <width-attr>
				    <height-attr>
				    <value-x-attr> 
				    <value-y-attr> 
				    <label-layout-type-attr> 
				    <control-object-state-attr>
				    <foreground-color-attr>
				    <setting-type-attr> 
				    <rows-attr> 
				    <columns-attr> 
				    <selection-required-attr>
				    <choices-attr> 
				    <choice-label-types-attr> 
				    <choice-colors-attr> 
				    <choice-defaults-attr> 
				    <initial-selections-attr>
				    <label-attr> 
				    <label-type-attr> 
				 *  <notify-handler-attr> 
				 *  <event-handler-attr> 
				    <user-data-attr>
				    <actions-attr>

<slider>			::= :type :slider
				    <name-attr>
				    <owner-attr>
				    <help-attr>
				    <x-attr>
				    <y-attr>
				    <width-attr>
				    <height-attr>
				    <value-x-attr> 
				    <value-y-attr> 
				    <control-object-state-attr>
				    <slider-width-attr> 
				    <foreground-color-attr>
				    <label-attr> 
				    <label-type-attr> 
				    <label-layout-type-attr> 
				    <orientation-attr>
				    <show-endboxes-attr>
				    <show-range-attr>
				    <show-value-attr>
				    <min-value-attr> 
				    <max-value-attr> 
				    <initial-integer-value-attr>
				 *  <notify-handler-attr> 
				 *  <event-handler-attr> 
				    <user-data-attr>
				    <actions-attr>

<stack>				::= :type :stack
				    <name-attr>
				    <owner-attr>
				    <members-attr>
				    <user-data-attr>


<term-pane>			::= :type :term-pane
				    <name-attr>
				    <owner-attr>
				    <help-attr>
				    <x-attr>
				    <y-attr>
				    <width-attr>
				    <height-attr>
				    <background-color-attr>
				    <foreground-color-attr>
				    <pane-state-attr>
				    <show-border-attr>
				 *  <event-handler-attr> 
				    <user-data-attr>
				    <actions-attr>

<text-field>		    	::= :type :text-field
				    <name-attr>
				    <owner-attr>
				    <help-attr>
				    <x-attr>
				    <y-attr>
				    <width-attr>
				    <height-attr>
				    <foreground-color-attr>
				    <text-type-attr> 
				    <label-attr> 
				    <label-type-attr> 
				    <value-x-attr> 
				    <value-y-attr> 
				    <label-layout-type-attr> 
				    <control-object-state-attr>
				    <rows-attr>
				    <value-length-attr> 
				    <stored-length-attr>
				    <value-underlined-attr>
				    <max-value-attr> 
				    <min-value-attr> 
				    <text-initial-value-attr>
				    <read-only-attr> 
				 *  <notify-handler-attr> 
				 *  <event-handler-attr> 
				    <user-data-attr>
				    <actions-attr>

<text-pane>			::= :type :text-pane
				    <name-attr>
				    <owner-attr>
				    <help-attr>
				    <x-attr>
				    <y-attr>
				    <width-attr>
				    <height-attr>
				    <background-color-attr>
				    <foreground-color-attr>
				    <pane-state-attr>
				    <show-border-attr>
				    <read-only-attr> 
				 *  <event-handler-attr> 
				    <user-data-attr>
				    <actions-attr>

<object>			::= (
				    <base-window> |
				    <button> |
				    <canvas-pane> |
				    <control-area> |
				    <drop_target> |
				    <gauge> |
				    <group> |
				    <menu> |
				    <message> |
				    <popup-window> |
				    <scrolling-list> |
				    <setting> |
				    <slider> |
				    <stack> |
				    <term-pane> |
				    <text-field> |
				    <text-pane> |
				    )

<object-list>			::= ( { <object> } )

<gil-file>			::= <gil-header> <object-list>


NOTES:
------

Windows (base and popup) can only contain panes (control area, text
pane, term pane).

Panes are always within windows.

Controls are always within control areas.

GIL objects are always ordered so containing objects come first.  Objects
are then ordered by Y location and then X location.

Window names must be unique within a GIL file.  Other object names
must be unique only within their containing window.


*   Attributes marked with an asterisk (*).
    In Devguide 3.0 this attribute is maintained for compatibility.
    The functionality has been replaced with the new :actions syntax.
    This attribute may be removed in a future release. The table below
    lists that attributes affected by the change.

**  Attributes marked with a double asterisk (**).
    In Devguide 3.0 this attribute is no longer used. The functionality
    has been replaced as indicated in the table below.

*** Attributes marked with a triple asterisk (***).
    Many attributes have been replaced with the new :actions syntax.
    The possible values for the :when, :function_type, :arg_type, and
    the :action vary from toolkit to toolkit. The .config files
    (XView.config, OLIT.config, TNT.config) in $GUIDEHOME/lib show the
    possible values that these each of these attributes can have for
    each type of object.  In addition, the events-attr has been
    replaced with the :events attribute in the .config files which list
    possible events for each object.

Following is a table that help clarify how the information stored in
the obsoleted versions of the attributes is now represented.

     Original Attribute			           New attribute(s)
   ----------------------			--------------------------

   *  done-handler-attr				actions-attr

   ** events-attr				actions-attr

   *  event-handler-attr			actions-attr

   *  notify-handler-attr			actions-attr

   ** mapped-attr				base-window-state-attr
						popup-window-state-attr

   *  menu-name-attr				actions-attr

   *  menu-handler-attr				actions-attr

   *  menu-item-handlers-attr			actions-attr

   *  repaint-proc-attr				actions-attr
